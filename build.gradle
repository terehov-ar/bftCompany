plugins {
    id 'java'
    id 'io.qameta.allure' version '3.0.1'
}

repositories {
    mavenCentral()
}

allure {
    report {
        version.set("2.29.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.29.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {
    testImplementation(
            'com.codeborne:selenide:7.11.0',
            "org.junit.jupiter:junit-jupiter:5.10.0",
            'com.github.javafaker:javafaker:1.0.2',
            'org.slf4j:slf4j-simple:2.0.7',
            'io.qameta.allure:allure-selenide:2.29.0'
    )
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.register('smoke_test', Test) {
    useJUnitPlatform {
        includeTags('smoke')
    }
}